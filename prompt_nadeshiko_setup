# Nadeshiko
# prompt theme for zshprompt
# Put this file in $HOME/.zprezto/modules/prompt/functions
# and execute `prompt nadeshiko`

prompt_nadeshiko_precmd () {
  vcs_info
}

prompt_nadeshiko_setup () {

  setopt LOCAL_OPTIONS
  unsetopt XTRACE KSH_ARRAYS
  prompt_opts=(cr percent subst)

  # Load required functions
  autoload -Uz vcs_info
  autoload -Uz add-zsh-hook
  autoload -Uz prompt_special_chars
  autoload -Uz colors

  # Add hook for calling vcs_info before each command
  add-zsh-hook precmd prompt_nadeshiko_precmd

  # Set vcs_info parameters.
  zstyle ':vcs_info:*' enable git
  zstyle ':vcs_info:git:*' check-for-changes true
  zstyle ':vcs_info:git:*' stagedstr '%F{122}!%f'
  # zstyle ':vcs_info:git:*' stagedstr '%F{243} %K{white}⮀%f%k%K{white}%F{red} ✔ %k%F{white}⮀%f%k'
  zstyle ':vcs_info:git:*' unstagedstr '%F{yellow}*%f'
  # zstyle ':vcs_info:git:*' unstagedstr '%F{243} %K{white}⮀%f%k%K{white}%F{red} ✎  %k%F{white}⮀%f%k'
  zstyle ':vcs_info:git:*' formats '%b%c%u'
  zstyle ':vcs_info:git:*' actionformats " %b%c%u|%F{cyan}%a%f"

  ACCENT='%K{190}  %k%K{black}%F{190}⮀%f%k'
  DIRECTORY="%K{black}%F{252} %~ %f%k%K{243}%F{black}⮀%f%k"
  VSC='%K{243}%F{white} ${vcs_info_msg_0_} %f%k%F{243}⮀%f '
  PROMPT=$ACCENT$DIRECTORY$VSC

  LAST='%F{237}⮂%f%K{237}  %(?.%F{122}✔%f.%F{161}✘%f) %f%k'
  HISTORY_NUM='%K{237}%F{243}⮂%f%k%K{243}%F{235} %! %f%k'
  TIME='%K{243}%F{white}⮂%f%K{white}%F{235} %D{%H:%M:%S} %f%k'
  RPROMPT=$LAST$HISTORY_NUM$TIME
}

prompt_nadeshiko_preview () {
  prompt_preview_theme nadeshiko "$@"
}

prompt_nadeshiko_setup "$@"
