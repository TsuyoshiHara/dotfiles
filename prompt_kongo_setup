# Kongo
# prompt theme for zshprompt
# Put this file in $HOME/.zprezto/modules/prompt/functions
# and execute `prompt kongo`

prompt_kongo_precmd () {
  vcs_info
}

prompt_kongo_setup () {

  setopt LOCAL_OPTIONS
  unsetopt XTRACE KSH_ARRAYS
  prompt_opts=(cr percent subst)

  # Load required functions
  autoload -Uz vcs_info
  autoload -Uz add-zsh-hook
  autoload -Uz prompt_special_chars
  autoload -Uz colors

  # Add hook for calling vcs_info before each command
  add-zsh-hook precmd prompt_kongo_precmd

  # Set vcs_info parameters.
  zstyle ':vcs_info:*' enable git
  zstyle ':vcs_info:git:*' check-for-changes true
  zstyle ':vcs_info:git:*' stagedstr '%F{122}!%f'
  # zstyle ':vcs_info:git:*' stagedstr '%F{243} %K{white}⮀%f%k%K{white}%F{red} ✔ %k%F{white}⮀%f%k'
  zstyle ':vcs_info:git:*' unstagedstr '%F{yellow}*%f'
  # zstyle ':vcs_info:git:*' unstagedstr '%F{243} %K{white}⮀%f%k%K{white}%F{red} ✎  %k%F{white}⮀%f%k'
  zstyle ':vcs_info:git:*' formats '%b%c%u'
  zstyle ':vcs_info:git:*' actionformats " %b%c%u|%F{cyan}%a%f"

  DIRECTORY="%K{61}%F{255} %~ %f%k%K{243}%F{61}⮀%f%k"
  VSC='%K{243}%F{235} ${vcs_info_msg_0_} %f%k%F{243}⮀%f '
  PROMPT=$DIRECTORY$VSC

  LAST='%F{237}⮂%f%K{237}  %(?.%F{61}✔%f.%F{161}✘%f) %f%k'
  HISTORY_NUM='%K{237}%F{243}⮂%f%k%K{243}%F{235} %! %f%k'
  TIME='%K{243}%F{white}⮂%f%K{white}%F{235} %D{%H:%M:%S} %f%k'
  RPROMPT=$LAST$HISTORY_NUM$TIME
}

prompt_kongo_preview () {
  prompt_preview_theme kongo "$@"
}

prompt_kongo_setup "$@"
